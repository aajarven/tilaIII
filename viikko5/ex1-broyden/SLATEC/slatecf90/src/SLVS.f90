subroutine SLVS (WM, IWM, X, TEM)
!
!! SLVS solves the linear system for the integrator package DEBDF.
!
!***SUBSIDIARY
!***PURPOSE  Subsidiary to DEBDF
!***LIBRARY   SLATEC
!***TYPE      SINGLE PRECISION (SLVS-S, DSLVS-D)
!***AUTHOR  Watts, H. A., (SNLA)
!***DESCRIPTION
!
!   SLVS solves the linear system in the iteration scheme for the
!   integrator package DEBDF.
!
!***SEE ALSO  DEBDF
!***ROUTINES CALLED  SGBSL, SGESL
!***COMMON BLOCKS    DEBDF1
!***REVISION HISTORY  (YYMMDD)
!   800901  DATE WRITTEN
!   891214  Prologue converted to Version 4.0 format.  (BAB)
!   900328  Added TYPE section.  (WRB)
!   910722  Updated AUTHOR section.  (ALS)
!   920422  Changed DIMENSION statement.  (WRB)
!***END PROLOGUE  SLVS
!
!LLL. OPTIMIZE
  INTEGER IWM, I, IER, IOWND, IOWNS, JSTART, KFLAG, L, MAXORD, &
     MEBAND, METH, MITER, ML, MU, N, NFE, NJE, NQ, NQU, NST
  REAL WM, X, TEM, &
     ROWND, ROWNS, EL0, H, HMIN, HMXI, HU, TN, UROUND, &
     DI, HL0, PHL0, R
  DIMENSION WM(*), IWM(*), X(*), TEM(*)
  COMMON /DEBDF1/ ROWND, ROWNS(210), &
     EL0, H, HMIN, HMXI, HU, TN, UROUND, IOWND(14), IOWNS(6), &
     IER, JSTART, KFLAG, L, METH, MITER, MAXORD, N, NQ, NST, NFE, &
     NJE, NQU
!-----------------------------------------------------------------------
! THIS ROUTINE MANAGES THE SOLUTION OF THE LINEAR SYSTEM ARISING FROM
! A CHORD ITERATION.  IT IS CALLED BY STOD  if MITER  /=  0.
! if MITER IS 1 OR 2, IT CALLS SGESL TO ACCOMPLISH THIS.
! if MITER = 3 IT UPDATES THE COEFFICIENT H*EL0 IN THE DIAGONAL
! MATRIX, AND THEN COMPUTES THE SOLUTION.
! if MITER IS 4 OR 5, IT CALLS SGBSL.
! COMMUNICATION WITH SLVS USES THE FOLLOWING VARIABLES..
! WM  = REAL WORK SPACE CONTAINING THE INVERSE DIAGONAL MATRIX if MITER
!       IS 3 AND THE LU DECOMPOSITION OF THE MATRIX OTHERWISE.
!       STORAGE OF MATRIX ELEMENTS STARTS AT WM(3).
!       WM ALSO CONTAINS THE FOLLOWING MATRIX-RELATED DATA..
!       WM(1) = SQRT(UROUND) (NOT USED HERE),
!       WM(2) = HL0, THE PREVIOUS VALUE OF H*EL0, USED if MITER = 3.
! IWM = INTEGER WORK SPACE CONTAINING PIVOT INFORMATION, STARTING AT
!       IWM(21), if MITER IS 1, 2, 4, OR 5.  IWM ALSO CONTAINS THE
!       BAND PARAMETERS ML = IWM(1) AND MU = IWM(2) if MITER IS 4 OR 5.
! X   = THE RIGHT-HAND SIDE VECTOR ON INPUT, AND THE SOLUTION VECTOR
!       ON OUTPUT, OF LENGTH N.
! TEM = VECTOR OF WORK SPACE OF LENGTH N, NOT USED IN THIS VERSION.
! IER = OUTPUT FLAG (IN COMMON).  IER = 0 if NO TROUBLE OCCURRED.
!       IER = -1 if A SINGULAR MATRIX AROSE WITH MITER = 3.
! THIS ROUTINE ALSO USES THE COMMON VARIABLES EL0, H, MITER, AND N.
!-----------------------------------------------------------------------
!***FIRST EXECUTABLE STATEMENT  SLVS
  IER = 0
  go to (100, 100, 300, 400, 400), MITER
 100  call SGESL (WM(3), N, N, IWM(21), X, 0)
  return
!
 300  PHL0 = WM(2)
  HL0 = H*EL0
  WM(2) = HL0
  if (HL0  ==  PHL0) go to 330
  R = HL0/PHL0
  DO 320 I = 1,N
    DI = 1.0E0 - R*(1.0E0 - 1.0E0/WM(I+2))
    if (ABS(DI)  ==  0.0E0) go to 390
 320    WM(I+2) = 1.0E0/DI
 330  DO 340 I = 1,N
 340    X(I) = WM(I+2)*X(I)
  return
 390  IER = -1
  return
!
 400  ML = IWM(1)
  MU = IWM(2)
  MEBAND = 2*ML + MU + 1
  call SGBSL (WM(3), MEBAND, N, ML, MU, IWM(21), X, 0)
  return
!----------------------- END OF SUBROUTINE SLVS -----------------------
end
