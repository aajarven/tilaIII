subroutine SDAJAC (NEQ, X, Y, YPRIME, DELTA, CJ, H, IER, WT, E, &
     WM, IWM, RES, IRES, UROUND, JAC, RPAR, IPAR, NTEMP)
!
!! SDAJAC computes and LU factors the iteration matrix for SDASSL.
!
!***LIBRARY   SLATEC (DASSL)
!***TYPE      SINGLE PRECISION (SDAJAC-S, DDAJAC-D)
!***AUTHOR  Petzold, Linda R., (LLNL)
!***DESCRIPTION
!-----------------------------------------------------------------------
!     THIS ROUTINE COMPUTES THE ITERATION MATRIX
!     PD=DG/DY+CJ*DG/DYPRIME (WHERE G(X,Y,YPRIME)=0).
!     HERE PD IS COMPUTED BY THE USER-SUPPLIED
!     ROUTINE JAC if IWM(MTYPE) IS 1 OR 4, AND
!     IT IS COMPUTED BY NUMERICAL FINITE DIFFERENCING
!     if IWM(MTYPE)IS 2 OR 5
!     THE PARAMETERS HAVE THE FOLLOWING MEANINGS.
!     Y        = ARRAY CONTAINING PREDICTED VALUES
!     YPRIME   = ARRAY CONTAINING PREDICTED DERIVATIVES
!     DELTA    = RESIDUAL EVALUATED AT (X,Y,YPRIME)
!                (USED ONLY if IWM(MTYPE)=2 OR 5)
!     CJ       = SCALAR PARAMETER DEFINING ITERATION MATRIX
!     H        = CURRENT STEPSIZE IN INTEGRATION
!     IER      = VARIABLE WHICH IS  /=  0
!                if ITERATION MATRIX IS SINGULAR,
!                AND 0 OTHERWISE.
!     WT       = VECTOR OF WEIGHTS FOR COMPUTING NORMS
!     E        = WORK SPACE (TEMPORARY) OF LENGTH NEQ
!     WM       = REAL WORK SPACE FOR MATRICES. ON
!                OUTPUT IT CONTAINS THE LU DECOMPOSITION
!                OF THE ITERATION MATRIX.
!     IWM      = INTEGER WORK SPACE CONTAINING
!                MATRIX INFORMATION
!     RES      = NAME OF THE EXTERNAL USER-SUPPLIED ROUTINE
!                TO EVALUATE THE RESIDUAL FUNCTION G(X,Y,YPRIME)
!     IRES     = FLAG WHICH IS EQUAL TO ZERO if NO ILLEGAL VALUES
!                IN RES, AND LESS THAN ZERO OTHERWISE.  (IF IRES
!                IS LESS THAN ZERO, THE MATRIX WAS NOT COMPLETED)
!                IN THIS CASE (IF IRES  <  0), THEN IER = 0.
!     UROUND   = THE UNIT ROUNDOFF ERROR OF THE MACHINE BEING USED.
!     JAC      = NAME OF THE EXTERNAL USER-SUPPLIED ROUTINE
!                TO EVALUATE THE ITERATION MATRIX (THIS ROUTINE
!                IS ONLY USED if IWM(MTYPE) IS 1 OR 4)
!-----------------------------------------------------------------------
!***ROUTINES CALLED  SGBFA, SGEFA
!***REVISION HISTORY  (YYMMDD)
!   830315  DATE WRITTEN
!   901009  Finished conversion to SLATEC 4.0 format (F.N.Fritsch)
!   901010  Modified three MAX calls to be all on one line.  (FNF)
!   901019  Merged changes made by C. Ulrich with SLATEC 4.0 format.
!   901026  Added explicit declarations for all variables and minor
!           cosmetic changes to prologue.  (FNF)
!   901101  Corrected PURPOSE.  (FNF)
!***END PROLOGUE  SDAJAC
!
  INTEGER  NEQ, IER, IWM(*), IRES, IPAR(*), NTEMP
  REAL  X, Y(*), YPRIME(*), DELTA(*), CJ, H, WT(*), E(*), WM(*), &
     UROUND, RPAR(*)
  EXTERNAL  RES, JAC
!
  EXTERNAL  SGBFA, SGEFA
!
  INTEGER  I, I1, I2, II, IPSAVE, ISAVE, J, K, L, LENPD, LIPVT, &
     LML, LMTYPE, LMU, MBA, MBAND, MEB1, MEBAND, MSAVE, MTYPE, N, &
     NPD, NPDM1, NROW
  REAL  DEL, DELINV, SQUR, YPSAVE, YSAVE
!
  PARAMETER (NPD=1)
  PARAMETER (LML=1)
  PARAMETER (LMU=2)
  PARAMETER (LMTYPE=4)
  PARAMETER (LIPVT=21)
!
!***FIRST EXECUTABLE STATEMENT  SDAJAC
  IER = 0
  NPDM1=NPD-1
  MTYPE=IWM(LMTYPE)
  go to (100,200,300,400,500),MTYPE
!
!
!     DENSE USER-SUPPLIED MATRIX
100   LENPD=NEQ*NEQ
  DO 110 I=1,LENPD
110      WM(NPDM1+I)=0.0E0
  call JAC(X,Y,YPRIME,WM(NPD),CJ,RPAR,IPAR)
  go to 230
!
!
!     DENSE FINITE-DIFFERENCE-GENERATED MATRIX
200   IRES=0
  NROW=NPDM1
  SQUR = SQRT(UROUND)
  DO 210 I=1,NEQ
     DEL=SQUR*MAX(ABS(Y(I)),ABS(H*YPRIME(I)),ABS(WT(I)))
     DEL=SIGN(DEL,H*YPRIME(I))
     DEL=(Y(I)+DEL)-Y(I)
     YSAVE=Y(I)
     YPSAVE=YPRIME(I)
     Y(I)=Y(I)+DEL
     YPRIME(I)=YPRIME(I)+CJ*DEL
     call RES(X,Y,YPRIME,E,IRES,RPAR,IPAR)
     if (IRES  <  0) RETURN
     DELINV=1.0E0/DEL
     DO 220 L=1,NEQ
220      WM(NROW+L)=(E(L)-DELTA(L))*DELINV
  NROW=NROW+NEQ
  Y(I)=YSAVE
  YPRIME(I)=YPSAVE
210   CONTINUE
!
!
!     DO DENSE-MATRIX LU DECOMPOSITION ON PD
230      call SGEFA(WM(NPD),NEQ,NEQ,IWM(LIPVT),IER)
  return
!
!
!     DUMMY SECTION FOR IWM(MTYPE)=3
300   return
!
!
!     BANDED USER-SUPPLIED MATRIX
400   LENPD=(2*IWM(LML)+IWM(LMU)+1)*NEQ
  DO 410 I=1,LENPD
410      WM(NPDM1+I)=0.0E0
  call JAC(X,Y,YPRIME,WM(NPD),CJ,RPAR,IPAR)
  MEBAND=2*IWM(LML)+IWM(LMU)+1
  go to 550
!
!
!     BANDED FINITE-DIFFERENCE-GENERATED MATRIX
500   MBAND=IWM(LML)+IWM(LMU)+1
  MBA=MIN(MBAND,NEQ)
  MEBAND=MBAND+IWM(LML)
  MEB1=MEBAND-1
  MSAVE=(NEQ/MBAND)+1
  ISAVE=NTEMP-1
  IPSAVE=ISAVE+MSAVE
  IRES=0
  SQUR=SQRT(UROUND)
  DO 540 J=1,MBA
     DO 510 N=J,NEQ,MBAND
      K= (N-J)/MBAND + 1
      WM(ISAVE+K)=Y(N)
      WM(IPSAVE+K)=YPRIME(N)
      DEL=SQUR*MAX(ABS(Y(N)),ABS(H*YPRIME(N)),ABS(WT(N)))
      DEL=SIGN(DEL,H*YPRIME(N))
      DEL=(Y(N)+DEL)-Y(N)
      Y(N)=Y(N)+DEL
510       YPRIME(N)=YPRIME(N)+CJ*DEL
  call RES(X,Y,YPRIME,E,IRES,RPAR,IPAR)
  if (IRES  <  0) RETURN
  DO 530 N=J,NEQ,MBAND
      K= (N-J)/MBAND + 1
      Y(N)=WM(ISAVE+K)
      YPRIME(N)=WM(IPSAVE+K)
      DEL=SQUR*MAX(ABS(Y(N)),ABS(H*YPRIME(N)),ABS(WT(N)))
      DEL=SIGN(DEL,H*YPRIME(N))
      DEL=(Y(N)+DEL)-Y(N)
      DELINV=1.0E0/DEL
      I1=MAX(1,(N-IWM(LMU)))
      I2=MIN(NEQ,(N+IWM(LML)))
      II=N*MEB1-IWM(LML)+NPDM1
      DO 520 I=I1,I2
520         WM(II+I)=(E(I)-DELTA(I))*DELINV
530      CONTINUE
540   CONTINUE
!
!
!     DO LU DECOMPOSITION OF BANDED PD
550   call SGBFA(WM(NPD),MEBAND,NEQ, &
      IWM(LML),IWM(LMU),IWM(LIPVT),IER)
  return
!------END OF SUBROUTINE SDAJAC------
end
