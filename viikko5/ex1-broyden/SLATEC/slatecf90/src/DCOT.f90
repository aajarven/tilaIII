FUNCTION DCOT (X)
!
!! DCOT computes the cotangent.
!
!***LIBRARY   SLATEC (FNLIB)
!***CATEGORY  C4A
!***TYPE      DOUBLE PRECISION (COT-S, DCOT-D, CCOT-C)
!***KEYWORDS  COTANGENT, ELEMENTARY FUNCTIONS, FNLIB, TRIGONOMETRIC
!***AUTHOR  Fullerton, W., (LANL)
!***DESCRIPTION
!
! DCOT(X) calculates the double precision trigonometric cotangent
! for double precision argument X.  X is in units of radians.
!
! Series for COT        on the interval  0.          to  6.25000E-02
!                                        with weighted error   5.52E-34
!                                         log weighted error  33.26
!                               significant figures required  32.34
!                                    decimal places required  33.85
!
!***REFERENCES  (NONE)
!***ROUTINES CALLED  D1MACH, DCSEVL, INITDS, XERMSG
!***REVISION HISTORY  (YYMMDD)
!   770601  DATE WRITTEN
!   890531  Changed all specific intrinsics to generic.  (WRB)
!   890531  REVISION DATE from Version 3.2
!   891214  Prologue converted to Version 4.0 format.  (BAB)
!   900315  CALLs to XERROR changed to CALLs to XERMSG.  (THJ)
!   920618  Removed space from variable names.  (RWC, WRB)
!***END PROLOGUE  DCOT
  DOUBLE PRECISION DCOT
  DOUBLE PRECISION X, COTCS(15), AINTY, AINTY2, PI2REC, SQEPS, &
    XMAX, XMIN, XSML, Y, YREM, PRODBG, DCSEVL, D1MACH
  LOGICAL FIRST
  SAVE COTCS, PI2REC, NTERMS, XMAX, XSML, XMIN, SQEPS, FIRST
  DATA COTCS(  1) / +.240259160982956302509553617744970D+0    /
  DATA COTCS(  2) / -.165330316015002278454746025255758D-1    /
  DATA COTCS(  3) / -.429983919317240189356476228239895D-4    /
  DATA COTCS(  4) / -.159283223327541046023490851122445D-6    /
  DATA COTCS(  5) / -.619109313512934872588620579343187D-9    /
  DATA COTCS(  6) / -.243019741507264604331702590579575D-11   /
  DATA COTCS(  7) / -.956093675880008098427062083100000D-14   /
  DATA COTCS(  8) / -.376353798194580580416291539706666D-16   /
  DATA COTCS(  9) / -.148166574646746578852176794666666D-18   /
  DATA COTCS( 10) / -.583335658903666579477984000000000D-21   /
  DATA COTCS( 11) / -.229662646964645773928533333333333D-23   /
  DATA COTCS( 12) / -.904197057307483326719999999999999D-26   /
  DATA COTCS( 13) / -.355988551920600064000000000000000D-28   /
  DATA COTCS( 14) / -.140155139824298666666666666666666D-30   /
  DATA COTCS( 15) / -.551800436872533333333333333333333D-33   /
  DATA PI2REC / .011619772367581343075535053490057D0 /
  DATA FIRST /.TRUE./
!***FIRST EXECUTABLE STATEMENT  DCOT
  if (FIRST) THEN
     NTERMS = INITDS (COTCS, 15, 0.1*REAL(D1MACH(3)) )
     XMAX = 1.0D0/D1MACH(4)
     XSML = SQRT(3.0D0*D1MACH(3))
     XMIN = EXP (MAX(LOG(D1MACH(1)), -LOG(D1MACH(2))) + 0.01D0)
     SQEPS = SQRT(D1MACH(4))
  end if
  FIRST = .FALSE.
!
  Y = ABS(X)
  if (Y  <  XMIN) call XERMSG ('SLATEC', 'DCOT', &
     'ABS(X) IS ZERO OR SO SMALL DCOT OVERFLOWS', 2, 2)
  if (Y  >  XMAX) call XERMSG ('SLATEC', 'DCOT', &
     'NO PRECISION BECAUSE ABS(X) IS TOO BIG', 3, 2)
!
! CAREFULLY COMPUTE Y * (2/PI) = (AINT(Y) + REM(Y)) * (.625 + PI2REC)
! = AINT(.625*Y) + REM(.625*Y) + Y*PI2REC  =  AINT(.625*Y) + Z
! = AINT(.625*Y) + AINT(Z) + REM(Z)
!
  AINTY = AINT (Y)
  YREM = Y - AINTY
  PRODBG = 0.625D0*AINTY
  AINTY = AINT (PRODBG)
  Y = (PRODBG-AINTY) + 0.625D0*YREM + PI2REC*Y
  AINTY2 = AINT (Y)
  AINTY = AINTY + AINTY2
  Y = Y - AINTY2
!
  IFN = MOD (AINTY, 2.0D0)
  if (IFN == 1) Y = 1.0D0 - Y
!
  if (ABS(X)  >  0.5D0 .AND. Y  <  ABS(X)*SQEPS) call XERMSG &
     ('SLATEC', 'DCOT', &
     'ANSWER LT HALF PRECISION, ABS(X) TOO BIG OR X NEAR N*PI ' // &
     '(N /= 0)', 1, 1)
!
  if (Y > 0.25D0) go to 20
  DCOT = 1.0D0/X
  if (Y > XSML) DCOT = (0.5D0 + DCSEVL (32.0D0*Y*Y-1.D0, COTCS, &
    NTERMS)) / Y
  go to 40
!
 20   if (Y > 0.5D0) go to 30
  DCOT = (0.5D0 + DCSEVL (8.D0*Y*Y-1.D0, COTCS, NTERMS))/(0.5D0*Y)
  DCOT = (DCOT*DCOT-1.D0)*0.5D0/DCOT
  go to 40
!
 30   DCOT = (0.5D0 + DCSEVL (2.D0*Y*Y-1.D0, COTCS, NTERMS))/(.25D0*Y)
  DCOT = (DCOT*DCOT-1.D0)*0.5D0/DCOT
  DCOT = (DCOT*DCOT-1.D0)*0.5D0/DCOT
!
 40   if (X /= 0.D0) DCOT = SIGN (DCOT, X)
  if (IFN == 1) DCOT = -DCOT
!
  return
end
