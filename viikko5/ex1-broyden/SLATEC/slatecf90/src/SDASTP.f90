subroutine SDASTP (X, Y, YPRIME, NEQ, RES, JAC, H, WT, JSTART, &
     IDID, RPAR, IPAR, PHI, DELTA, E, WM, IWM, ALPHA, BETA, GAMMA, &
     PSI, SIGMA, CJ, CJOLD, HOLD, S, HMIN, UROUND, IPHASE, JCALC, K, &
     KOLD, NS, NONNEG, NTEMP)
!
!! SDASTP performs one step of the SDASSL integration.
!
!***LIBRARY   SLATEC (DASSL)
!***TYPE      SINGLE PRECISION (SDASTP-S, DDASTP-D)
!***AUTHOR  Petzold, Linda R., (LLNL)
!***DESCRIPTION
!-----------------------------------------------------------------------
!     SDASTP SOLVES A SYSTEM OF DIFFERENTIAL/
!     ALGEBRAIC EQUATIONS OF THE FORM
!     G(X,Y,YPRIME) = 0,  FOR ONE STEP (NORMALLY
!     FROM X TO X+H).
!
!     THE METHODS USED ARE MODIFIED DIVIDED
!     DIFFERENCE,FIXED LEADING COEFFICIENT
!     FORMS OF BACKWARD DIFFERENTIATION
!     FORMULAS. THE CODE ADJUSTS THE STEPSIZE
!     AND ORDER TO CONTROL THE LOCAL ERROR PER
!     STEP.
!
!
!     THE PARAMETERS REPRESENT
!     X  --        INDEPENDENT VARIABLE
!     Y  --        SOLUTION VECTOR AT X
!     YPRIME --    DERIVATIVE OF SOLUTION VECTOR
!                  AFTER SUCCESSFUL STEP
!     NEQ --       NUMBER OF EQUATIONS TO BE INTEGRATED
!     RES --       EXTERNAL USER-SUPPLIED SUBROUTINE
!                  TO EVALUATE THE RESIDUAL.  THE call IS
!                  call RES(X,Y,YPRIME,DELTA,IRES,RPAR,IPAR)
!                  X,Y,YPRIME ARE INPUT.  DELTA IS OUTPUT.
!                  ON INPUT, IRES=0.  RES SHOULD ALTER IRES ONLY
!                  if IT ENCOUNTERS AN ILLEGAL VALUE OF Y OR A
!                  STOP CONDITION.  SET IRES=-1 if AN INPUT VALUE
!                  OF Y IS ILLEGAL, AND SDASTP WILL TRY TO SOLVE
!                  THE PROBLEM WITHOUT GETTING IRES = -1.  IF
!                  IRES=-2, SDASTP RETURNS CONTROL TO THE CALLING
!                  PROGRAM WITH IDID = -11.
!     JAC --       EXTERNAL USER-SUPPLIED ROUTINE TO EVALUATE
!                  THE ITERATION MATRIX (THIS IS OPTIONAL)
!                  THE call IS OF THE FORM
!                  call JAC(X,Y,YPRIME,PD,CJ,RPAR,IPAR)
!                  PD IS THE MATRIX OF PARTIAL DERIVATIVES,
!                  PD=DG/DY+CJ*DG/DYPRIME
!     H --         APPROPRIATE STEP SIZE FOR NEXT STEP.
!                  NORMALLY DETERMINED BY THE CODE
!     WT --        VECTOR OF WEIGHTS FOR ERROR CRITERION.
!     JSTART --    INTEGER VARIABLE SET 0 FOR
!                  FIRST STEP, 1 OTHERWISE.
!     IDID --      COMPLETION CODE WITH THE FOLLOWING MEANINGS:
!                  IDID= 1 -- THE STEP WAS COMPLETED SUCCESSFULLY
!                  IDID=-6 -- THE ERROR TEST FAILED REPEATEDLY
!                  IDID=-7 -- THE CORRECTOR COULD NOT CONVERGE
!                  IDID=-8 -- THE ITERATION MATRIX IS SINGULAR
!                  IDID=-9 -- THE CORRECTOR COULD NOT CONVERGE.
!                             THERE WERE REPEATED ERROR TEST
!                             FAILURES ON THIS STEP.
!                  IDID=-10-- THE CORRECTOR COULD NOT CONVERGE
!                             BECAUSE IRES WAS EQUAL TO MINUS ONE
!                  IDID=-11-- IRES EQUAL TO -2 WAS ENCOUNTERED,
!                             AND CONTROL IS BEING RETURNED TO
!                             THE CALLING PROGRAM
!     RPAR,IPAR -- REAL AND INTEGER PARAMETER ARRAYS THAT
!                  ARE USED FOR COMMUNICATION BETWEEN THE
!                  CALLING PROGRAM AND EXTERNAL USER ROUTINES
!                  THEY ARE NOT ALTERED BY SDASTP
!     PHI --       ARRAY OF DIVIDED DIFFERENCES USED BY
!                  SDASTP. THE LENGTH IS NEQ*(K+1),WHERE
!                  K IS THE MAXIMUM ORDER
!     DELTA,E --   WORK VECTORS FOR SDASTP OF LENGTH NEQ
!     WM,IWM --    REAL AND INTEGER ARRAYS STORING
!                  MATRIX INFORMATION SUCH AS THE MATRIX
!                  OF PARTIAL DERIVATIVES,PERMUTATION
!                  VECTOR, AND VARIOUS OTHER INFORMATION.
!
!     THE OTHER PARAMETERS ARE INFORMATION
!     WHICH IS NEEDED INTERNALLY BY SDASTP TO
!     CONTINUE FROM STEP TO STEP.
!
!-----------------------------------------------------------------------
!***ROUTINES CALLED  SDAJAC, SDANRM, SDASLV, SDATRP
!***REVISION HISTORY  (YYMMDD)
!   830315  DATE WRITTEN
!   901009  Finished conversion to SLATEC 4.0 format (F.N.Fritsch)
!   901019  Merged changes made by C. Ulrich with SLATEC 4.0 format.
!   901026  Added explicit declarations for all variables and minor
!           cosmetic changes to prologue.  (FNF)
!***END PROLOGUE  SDASTP
!
  INTEGER  NEQ, JSTART, IDID, IPAR(*), IWM(*), IPHASE, JCALC, K, &
     KOLD, NS, NONNEG, NTEMP
  REAL  X, Y(*), YPRIME(*), H, WT(*), RPAR(*), PHI(NEQ,*), DELTA(*), &
     E(*), WM(*), ALPHA(*), BETA(*), GAMMA(*), PSI(*), SIGMA(*), CJ, &
     CJOLD, HOLD, S, HMIN, UROUND
  EXTERNAL  RES, JAC
!
  EXTERNAL  SDAJAC, SDANRM, SDASLV, SDATRP
  REAL  SDANRM
!
  INTEGER  I, IER, IRES, J, J1, KDIFF, KM1, KNEW, KP1, KP2, LCTF, &
     LETF, LMXORD, LNJE, LNRE, LNST, M, MAXIT, NCF, NEF, NSF, NSP1
  REAL  ALPHA0, ALPHAS, CJLAST, CK, DELNRM, ENORM, ERK, ERKM1, &
     ERKM2, ERKP1, ERR, EST, HNEW, OLDNRM, PNORM, R, RATE, TEMP1, &
     TEMP2, TERK, TERKM1, TERKM2, TERKP1, XOLD, XRATE
  LOGICAL  CONVGD
!
  PARAMETER (LMXORD=3)
  PARAMETER (LNST=11)
  PARAMETER (LNRE=12)
  PARAMETER (LNJE=13)
  PARAMETER (LETF=14)
  PARAMETER (LCTF=15)
!
  DATA MAXIT/4/
  DATA XRATE/0.25E0/
!
!
!
!
!
!-----------------------------------------------------------------------
!     BLOCK 1.
!     INITIALIZE. ON THE FIRST CALL,SET
!     THE ORDER TO 1 AND INITIALIZE
!     OTHER VARIABLES.
!-----------------------------------------------------------------------
!
!     INITIALIZATIONS FOR ALL CALLS
!***FIRST EXECUTABLE STATEMENT  SDASTP
  IDID=1
  XOLD=X
  NCF=0
  NSF=0
  NEF=0
  if ( JSTART  /=  0) go to 120
!
!     if THIS IS THE FIRST STEP,PERFORM
!     OTHER INITIALIZATIONS
  IWM(LETF) = 0
  IWM(LCTF) = 0
  K=1
  KOLD=0
  HOLD=0.0E0
  JSTART=1
  PSI(1)=H
  CJOLD = 1.0E0/H
  CJ = CJOLD
  S = 100.E0
  JCALC = -1
  DELNRM=1.0E0
  IPHASE = 0
  NS=0
120   CONTINUE
!
!
!
!
!
!-----------------------------------------------------------------------
!     BLOCK 2
!     COMPUTE COEFFICIENTS OF FORMULAS FOR
!     THIS STEP.
!-----------------------------------------------------------------------
200   CONTINUE
  KP1=K+1
  KP2=K+2
  KM1=K-1
  XOLD=X
  if ( H /= HOLD.OR.K  /=  KOLD) NS = 0
  NS=MIN(NS+1,KOLD+2)
  NSP1=NS+1
  if ( KP1  <  NS)go to 230
!
  BETA(1)=1.0E0
  ALPHA(1)=1.0E0
  TEMP1=H
  GAMMA(1)=0.0E0
  SIGMA(1)=1.0E0
  DO 210 I=2,KP1
     TEMP2=PSI(I-1)
     PSI(I-1)=TEMP1
     BETA(I)=BETA(I-1)*PSI(I-1)/TEMP2
     TEMP1=TEMP2+H
     ALPHA(I)=H/TEMP1
     SIGMA(I)=(I-1)*SIGMA(I-1)*ALPHA(I)
     GAMMA(I)=GAMMA(I-1)+ALPHA(I-1)/H
210      CONTINUE
  PSI(KP1)=TEMP1
230   CONTINUE
!
!     COMPUTE ALPHAS, ALPHA0
  ALPHAS = 0.0E0
  ALPHA0 = 0.0E0
  DO 240 I = 1,K
    ALPHAS = ALPHAS - 1.0E0/I
    ALPHA0 = ALPHA0 - ALPHA(I)
240     CONTINUE
!
!     COMPUTE LEADING COEFFICIENT CJ
  CJLAST = CJ
  CJ = -ALPHAS/H
!
!     COMPUTE VARIABLE STEPSIZE ERROR COEFFICIENT CK
  CK = ABS(ALPHA(KP1) + ALPHAS - ALPHA0)
  CK = MAX(CK,ALPHA(KP1))
!
!     DECIDE WHETHER NEW JACOBIAN IS NEEDED
  TEMP1 = (1.0E0 - XRATE)/(1.0E0 + XRATE)
  TEMP2 = 1.0E0/TEMP1
  if (CJ/CJOLD  <  TEMP1 .OR. CJ/CJOLD  >  TEMP2) JCALC = -1
  if (CJ  /=  CJLAST) S = 100.E0
!
!     CHANGE PHI TO PHI STAR
  if ( KP1  <  NSP1) go to 280
  DO 270 J=NSP1,KP1
     DO 260 I=1,NEQ
260         PHI(I,J)=BETA(J)*PHI(I,J)
270      CONTINUE
280   CONTINUE
!
!     UPDATE TIME
  X=X+H
!
!
!
!
!
!-----------------------------------------------------------------------
!     BLOCK 3
!     PREDICT THE SOLUTION AND DERIVATIVE,
!     AND SOLVE THE CORRECTOR EQUATION
!-----------------------------------------------------------------------
!
!     FIRST,PREDICT THE SOLUTION AND DERIVATIVE
300   CONTINUE
  DO 310 I=1,NEQ
     Y(I)=PHI(I,1)
310      YPRIME(I)=0.0E0
  DO 330 J=2,KP1
     DO 320 I=1,NEQ
        Y(I)=Y(I)+PHI(I,J)
320         YPRIME(I)=YPRIME(I)+GAMMA(J)*PHI(I,J)
330   CONTINUE
  PNORM = SDANRM (NEQ,Y,WT,RPAR,IPAR)
!
!
!
!     SOLVE THE CORRECTOR EQUATION USING A
!     MODIFIED NEWTON SCHEME.
  CONVGD= .TRUE.
  M=0
  IWM(LNRE)=IWM(LNRE)+1
  IRES = 0
  call RES(X,Y,YPRIME,DELTA,IRES,RPAR,IPAR)
  if (IRES  <  0) go to 380
!
!
!     if INDICATED,REEVALUATE THE
!     ITERATION MATRIX PD = DG/DY + CJ*DG/DYPRIME
!     (WHERE G(X,Y,YPRIME)=0). SET
!     JCALC TO 0 AS AN INDICATOR THAT
!     THIS HAS BEEN DONE.
  if ( JCALC  /=  -1)go to 340
  IWM(LNJE)=IWM(LNJE)+1
  JCALC=0
  call SDAJAC(NEQ,X,Y,YPRIME,DELTA,CJ,H, &
   IER,WT,E,WM,IWM,RES,IRES,UROUND,JAC,RPAR, &
   IPAR,NTEMP)
  CJOLD=CJ
  S = 100.E0
  if (IRES  <  0) go to 380
  if ( IER  /=  0)go to 380
  NSF=0
!
!
!     INITIALIZE THE ERROR ACCUMULATION VECTOR E.
340   CONTINUE
  DO 345 I=1,NEQ
345      E(I)=0.0E0
!
!
!     CORRECTOR LOOP.
350   CONTINUE
!
!     MULTIPLY RESIDUAL BY TEMP1 TO ACCELERATE CONVERGENCE
  TEMP1 = 2.0E0/(1.0E0 + CJ/CJOLD)
  DO 355 I = 1,NEQ
355     DELTA(I) = DELTA(I) * TEMP1
!
!     COMPUTE A NEW ITERATE (BACK-SUBSTITUTION).
!     STORE THE CORRECTION IN DELTA.
  call SDASLV(NEQ,DELTA,WM,IWM)
!
!     UPDATE Y, E, AND YPRIME
  DO 360 I=1,NEQ
     Y(I)=Y(I)-DELTA(I)
     E(I)=E(I)-DELTA(I)
360      YPRIME(I)=YPRIME(I)-CJ*DELTA(I)
!
!     TEST FOR CONVERGENCE OF THE ITERATION
  DELNRM=SDANRM(NEQ,DELTA,WT,RPAR,IPAR)
  if (DELNRM  <=  100.E0*UROUND*PNORM) go to 375
  if (M  >  0) go to 365
     OLDNRM = DELNRM
     go to 367
365   RATE = (DELNRM/OLDNRM)**(1.0E0/M)
  if (RATE  >  0.90E0) go to 370
  S = RATE/(1.0E0 - RATE)
367   if (S*DELNRM  <=  0.33E0) go to 375
!
!     THE CORRECTOR HAS NOT YET CONVERGED.
!     UPDATE M AND TEST WHETHER THE
!     MAXIMUM NUMBER OF ITERATIONS HAVE
!     BEEN TRIED.
  M=M+1
  if ( M >= MAXIT)go to 370
!
!     EVALUATE THE RESIDUAL
!     AND GO BACK TO DO ANOTHER ITERATION
  IWM(LNRE)=IWM(LNRE)+1
  IRES = 0
  call RES(X,Y,YPRIME,DELTA,IRES, &
    RPAR,IPAR)
  if (IRES  <  0) go to 380
  go to 350
!
!
!     THE CORRECTOR FAILED TO CONVERGE IN MAXIT
!     ITERATIONS. if THE ITERATION MATRIX
!     IS NOT CURRENT,RE-DO THE STEP WITH
!     A NEW ITERATION MATRIX.
370   CONTINUE
  if ( JCALC == 0)go to 380
  JCALC=-1
  go to 300
!
!
!     THE ITERATION HAS CONVERGED.  if NONNEGATIVITY OF SOLUTION IS
!     REQUIRED, SET THE SOLUTION NONNEGATIVE, if THE PERTURBATION
!     TO DO IT IS SMALL ENOUGH.  if THE CHANGE IS TOO LARGE, THEN
!     CONSIDER THE CORRECTOR ITERATION TO HAVE FAILED.
375   if ( NONNEG  ==  0) go to 390
  DO 377 I = 1,NEQ
377      DELTA(I) = MIN(Y(I),0.0E0)
  DELNRM = SDANRM(NEQ,DELTA,WT,RPAR,IPAR)
  if ( DELNRM  >  0.33E0) go to 380
  DO 378 I = 1,NEQ
378      E(I) = E(I) - DELTA(I)
  go to 390
!
!
!     EXITS FROM BLOCK 3
!     NO CONVERGENCE WITH CURRENT ITERATION
!     MATRIX,OR SINGULAR ITERATION MATRIX
380   CONVGD= .FALSE.
390   JCALC = 1
  if ( .NOT.CONVGD)go to 600
!
!
!
!
!
!-----------------------------------------------------------------------
!     BLOCK 4
!     ESTIMATE THE ERRORS AT ORDERS K,K-1,K-2
!     AS if CONSTANT STEPSIZE WAS USED. ESTIMATE
!     THE LOCAL ERROR AT ORDER K AND TEST
!     WHETHER THE CURRENT STEP IS SUCCESSFUL.
!-----------------------------------------------------------------------
!
!     ESTIMATE ERRORS AT ORDERS K,K-1,K-2
  ENORM = SDANRM(NEQ,E,WT,RPAR,IPAR)
  ERK = SIGMA(K+1)*ENORM
  TERK = (K+1)*ERK
  EST = ERK
  KNEW=K
  if ( K  ==  1)go to 430
  DO 405 I = 1,NEQ
405     DELTA(I) = PHI(I,KP1) + E(I)
  ERKM1=SIGMA(K)*SDANRM(NEQ,DELTA,WT,RPAR,IPAR)
  TERKM1 = K*ERKM1
  if ( K  >  2)go to 410
  if ( TERKM1  <=  0.5E0*TERK)go to 420
  go to 430
410   CONTINUE
  DO 415 I = 1,NEQ
415     DELTA(I) = PHI(I,K) + DELTA(I)
  ERKM2=SIGMA(K-1)*SDANRM(NEQ,DELTA,WT,RPAR,IPAR)
  TERKM2 = (K-1)*ERKM2
  if ( MAX(TERKM1,TERKM2) > TERK)go to 430
!     LOWER THE ORDER
420   CONTINUE
  KNEW=K-1
  EST = ERKM1
!
!
!     CALCULATE THE LOCAL ERROR FOR THE CURRENT STEP
!     TO SEE if THE STEP WAS SUCCESSFUL
430   CONTINUE
  ERR = CK * ENORM
  if ( ERR  >  1.0E0)go to 600
!
!
!
!
!
!-----------------------------------------------------------------------
!     BLOCK 5
!     THE STEP IS SUCCESSFUL. DETERMINE
!     THE BEST ORDER AND STEPSIZE FOR
!     THE NEXT STEP. UPDATE THE DIFFERENCES
!     FOR THE NEXT STEP.
!-----------------------------------------------------------------------
  IDID=1
  IWM(LNST)=IWM(LNST)+1
  KDIFF=K-KOLD
  KOLD=K
  HOLD=H
!
!
!     ESTIMATE THE ERROR AT ORDER K+1 UNLESS:
!        ALREADY DECIDED TO LOWER ORDER, OR
!        ALREADY USING MAXIMUM ORDER, OR
!        STEPSIZE NOT CONSTANT, OR
!        ORDER RAISED IN PREVIOUS STEP
  if ( KNEW == KM1.OR.K == IWM(LMXORD))IPHASE=1
  if ( IPHASE  ==  0)go to 545
  if ( KNEW == KM1)go to 540
  if ( K == IWM(LMXORD)) go to 550
  if ( KP1 >= NS.OR.KDIFF == 1)go to 550
  DO 510 I=1,NEQ
510      DELTA(I)=E(I)-PHI(I,KP2)
  ERKP1 = (1.0E0/(K+2))*SDANRM(NEQ,DELTA,WT,RPAR,IPAR)
  TERKP1 = (K+2)*ERKP1
  if ( K > 1)go to 520
  if ( TERKP1 >= 0.5E0*TERK)go to 550
  go to 530
520   if ( TERKM1 <= MIN(TERK,TERKP1))go to 540
  if ( TERKP1 >= TERK.OR.K == IWM(LMXORD))go to 550
!
!     RAISE ORDER
530   K=KP1
  EST = ERKP1
  go to 550
!
!     LOWER ORDER
540   K=KM1
  EST = ERKM1
  go to 550
!
!     if IPHASE = 0, INCREASE ORDER BY ONE AND MULTIPLY STEPSIZE BY
!     FACTOR TWO
545   K = KP1
  HNEW = H*2.0E0
  H = HNEW
  go to 575
!
!
!     DETERMINE THE APPROPRIATE STEPSIZE FOR
!     THE NEXT STEP.
550   HNEW=H
  TEMP2=K+1
  R=(2.0E0*EST+0.0001E0)**(-1.0E0/TEMP2)
  if ( R  <  2.0E0) go to 555
  HNEW = 2.0E0*H
  go to 560
555   if ( R  >  1.0E0) go to 560
  R = MAX(0.5E0,MIN(0.9E0,R))
  HNEW = H*R
560   H=HNEW
!
!
!     UPDATE DIFFERENCES FOR NEXT STEP
575   CONTINUE
  if ( KOLD == IWM(LMXORD))go to 585
  DO 580 I=1,NEQ
580      PHI(I,KP2)=E(I)
585   CONTINUE
  DO 590 I=1,NEQ
590      PHI(I,KP1)=PHI(I,KP1)+E(I)
  DO 595 J1=2,KP1
     J=KP1-J1+1
     DO 595 I=1,NEQ
595      PHI(I,J)=PHI(I,J)+PHI(I,J+1)
  return
!
!
!
!
!
!-----------------------------------------------------------------------
!     BLOCK 6
!     THE STEP IS UNSUCCESSFUL. RESTORE X,PSI,PHI
!     DETERMINE APPROPRIATE STEPSIZE FOR
!     CONTINUING THE INTEGRATION, OR EXIT WITH
!     AN ERROR FLAG if THERE HAVE BEEN MANY
!     FAILURES.
!-----------------------------------------------------------------------
600   IPHASE = 1
!
!     RESTORE X,PHI,PSI
  X=XOLD
  if ( KP1 < NSP1)go to 630
  DO 620 J=NSP1,KP1
     TEMP1=1.0E0/BETA(J)
     DO 610 I=1,NEQ
610         PHI(I,J)=TEMP1*PHI(I,J)
620      CONTINUE
630   CONTINUE
  DO 640 I=2,KP1
640      PSI(I-1)=PSI(I)-H
!
!
!     TEST WHETHER FAILURE IS DUE TO CORRECTOR ITERATION
!     OR ERROR TEST
  if ( CONVGD)go to 660
  IWM(LCTF)=IWM(LCTF)+1
!
!
!     THE NEWTON ITERATION FAILED TO CONVERGE WITH
!     A CURRENT ITERATION MATRIX.  DETERMINE THE CAUSE
!     OF THE FAILURE AND TAKE APPROPRIATE ACTION.
  if ( IER == 0)go to 650
!
!     THE ITERATION MATRIX IS SINGULAR. REDUCE
!     THE STEPSIZE BY A FACTOR OF 4. IF
!     THIS HAPPENS THREE TIMES IN A ROW ON
!     THE SAME STEP, RETURN WITH AN ERROR FLAG
  NSF=NSF+1
  R = 0.25E0
  H=H*R
  if (NSF  <  3 .AND. ABS(H)  >=  HMIN) go to 690
  IDID=-8
  go to 675
!
!
!     THE NEWTON ITERATION FAILED TO CONVERGE FOR A REASON
!     OTHER THAN A SINGULAR ITERATION MATRIX.  if IRES = -2, THEN
!     return.  OTHERWISE, REDUCE THE STEPSIZE AND TRY AGAIN, UNLESS
!     TOO MANY FAILURES HAVE OCCURRED.
650   CONTINUE
  if (IRES  >  -2) go to 655
  IDID = -11
  go to 675
655   NCF = NCF + 1
  R = 0.25E0
  H = H*R
  if (NCF  <  10 .AND. ABS(H)  >=  HMIN) go to 690
  IDID = -7
  if (IRES  <  0) IDID = -10
  if (NEF  >=  3) IDID = -9
  go to 675
!
!
!     THE NEWTON SCHEME CONVERGED, AND THE CAUSE
!     OF THE FAILURE WAS THE ERROR ESTIMATE
!     EXCEEDING THE TOLERANCE.
660   NEF=NEF+1
  IWM(LETF)=IWM(LETF)+1
  if (NEF  >  1) go to 665
!
!     ON FIRST ERROR TEST FAILURE, KEEP CURRENT ORDER OR LOWER
!     ORDER BY ONE.  COMPUTE NEW STEPSIZE BASED ON DIFFERENCES
!     OF THE SOLUTION.
  K = KNEW
  TEMP2 = K + 1
  R = 0.90E0*(2.0E0*EST+0.0001E0)**(-1.0E0/TEMP2)
  R = MAX(0.25E0,MIN(0.9E0,R))
  H = H*R
  if (ABS(H)  >=  HMIN) go to 690
  IDID = -6
  go to 675
!
!     ON SECOND ERROR TEST FAILURE, USE THE CURRENT ORDER OR
!     DECREASE ORDER BY ONE.  REDUCE THE STEPSIZE BY A FACTOR OF
!     FOUR.
665   if (NEF  >  2) go to 670
  K = KNEW
  H = 0.25E0*H
  if (ABS(H)  >=  HMIN) go to 690
  IDID = -6
  go to 675
!
!     ON THIRD AND SUBSEQUENT ERROR TEST FAILURES, SET THE ORDER TO
!     ONE AND REDUCE THE STEPSIZE BY A FACTOR OF FOUR.
670   K = 1
  H = 0.25E0*H
  if (ABS(H)  >=  HMIN) go to 690
  IDID = -6
  go to 675
!
!
!
!
!     FOR ALL CRASHES, RESTORE Y TO ITS LAST VALUE,
!     INTERPOLATE TO FIND YPRIME AT LAST X, AND RETURN
675   CONTINUE
  call SDATRP(X,X,Y,YPRIME,NEQ,K,PHI,PSI)
  return
!
!
!     GO BACK AND TRY THIS STEP AGAIN
690   go to 200
!
!------END OF SUBROUTINE SDASTP------
end
