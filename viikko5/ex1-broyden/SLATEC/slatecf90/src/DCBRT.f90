DOUBLE PRECISION FUNCTION DCBRT (X)
!
!! DCBRT computes the cube root.
!
!***LIBRARY   SLATEC (FNLIB)
!***CATEGORY  C2
!***TYPE      DOUBLE PRECISION (CBRT-S, DCBRT-D, CCBRT-C)
!***KEYWORDS  CUBE ROOT, ELEMENTARY FUNCTIONS, FNLIB, ROOTS
!***AUTHOR  Fullerton, W., (LANL)
!***DESCRIPTION
!
! DCBRT(X) calculates the double precision cube root for
! double precision argument X.
!
!***REFERENCES  (NONE)
!***ROUTINES CALLED  D1MACH, D9PAK, D9UPAK
!***REVISION HISTORY  (YYMMDD)
!   770601  DATE WRITTEN
!   890531  Changed all specific intrinsics to generic.  (WRB)
!   890531  REVISION DATE from Version 3.2
!   891214  Prologue converted to Version 4.0 format.  (BAB)
!***END PROLOGUE  DCBRT
  DOUBLE PRECISION X, CBRT2(5), Y, CBRTSQ,  D9PAK, D1MACH
  SAVE CBRT2, NITER
  DATA CBRT2(1) / 0.62996052494743658238360530363911D0 /
  DATA CBRT2(2) / 0.79370052598409973737585281963615D0 /
  DATA CBRT2(3) / 1.0D0 /
  DATA CBRT2(4) / 1.25992104989487316476721060727823D0 /
  DATA CBRT2(5) / 1.58740105196819947475170563927231D0 /
  DATA NITER / 0 /
!***FIRST EXECUTABLE STATEMENT  DCBRT
  if (NITER == 0) NITER = 1.443*LOG(-.106*LOG(0.1*REAL(D1MACH(3))) &
    ) + 1.0
!
  DCBRT = 0.D0
  if (X == 0.D0) RETURN
!
  call D9UPAK (ABS(X), Y, N)
  IXPNT = N/3
  IREM = N - 3*IXPNT + 3
!
! THE APPROXIMATION BELOW IS A GENERALIZED CHEBYSHEV SERIES CONVERTED
! TO POLYNOMIAL FORM.  THE APPROX IS NEARLY BEST IN THE SENSE OF
! RELATIVE ERROR WITH 4.085 DIGITS ACCURACY.
!
  Z = Y
  DCBRT = .439581E0 + Z*(.928549E0 + Z*(-.512653E0 + Z*.144586E0))
!
  DO 10 ITER=1,NITER
    CBRTSQ = DCBRT*DCBRT
    DCBRT = DCBRT + (Y-DCBRT*CBRTSQ)/(3.D0*CBRTSQ)
 10   CONTINUE
!
  DCBRT = D9PAK (CBRT2(IREM)*SIGN(DCBRT,X), IXPNT)
  return
!
end
