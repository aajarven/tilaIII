function GAMMA (X)
!
!! GAMMA computes the complete Gamma function.
!
!***LIBRARY   SLATEC (FNLIB)
!***CATEGORY  C7A
!***TYPE      SINGLE PRECISION (GAMMA-S, DGAMMA-D, CGAMMA-C)
!***KEYWORDS  COMPLETE GAMMA FUNCTION, FNLIB, SPECIAL FUNCTIONS
!***AUTHOR  Fullerton, W., (LANL)
!***DESCRIPTION
!
! GAMMA computes the gamma function at X, where X is not 0, -1, -2, ....
! GAMMA and X are single precision.
!
!***REFERENCES  (NONE)
!***ROUTINES CALLED  CSEVL, GAMLIM, INITS, R1MACH, R9LGMC, XERMSG
!***REVISION HISTORY  (YYMMDD)
!   770601  DATE WRITTEN
!   890531  Changed all specific intrinsics to generic.  (WRB)
!   890531  REVISION DATE from Version 3.2
!   891214  Prologue converted to Version 4.0 format.  (BAB)
!   900315  CALLs to XERROR changed to CALLs to XERMSG.  (THJ)
!***END PROLOGUE  GAMMA
  DIMENSION GCS(23)
  LOGICAL FIRST
  SAVE GCS, PI, SQ2PIL, NGCS, XMIN, XMAX, DXREL, FIRST
  DATA GCS   ( 1) / .008571195590989331E0/
  DATA GCS   ( 2) / .004415381324841007E0/
  DATA GCS   ( 3) / .05685043681599363E0/
  DATA GCS   ( 4) /-.004219835396418561E0/
  DATA GCS   ( 5) / .001326808181212460E0/
  DATA GCS   ( 6) /-.0001893024529798880E0/
  DATA GCS   ( 7) / .0000360692532744124E0/
  DATA GCS   ( 8) /-.0000060567619044608E0/
  DATA GCS   ( 9) / .0000010558295463022E0/
  DATA GCS   (10) /-.0000001811967365542E0/
  DATA GCS   (11) / .0000000311772496471E0/
  DATA GCS   (12) /-.0000000053542196390E0/
  DATA GCS   (13) / .0000000009193275519E0/
  DATA GCS   (14) /-.0000000001577941280E0/
  DATA GCS   (15) / .0000000000270798062E0/
  DATA GCS   (16) /-.0000000000046468186E0/
  DATA GCS   (17) / .0000000000007973350E0/
  DATA GCS   (18) /-.0000000000001368078E0/
  DATA GCS   (19) / .0000000000000234731E0/
  DATA GCS   (20) /-.0000000000000040274E0/
  DATA GCS   (21) / .0000000000000006910E0/
  DATA GCS   (22) /-.0000000000000001185E0/
  DATA GCS   (23) / .0000000000000000203E0/
  DATA PI /3.14159265358979324E0/
! SQ2PIL IS LOG (SQRT (2.*PI) )
  DATA SQ2PIL /0.91893853320467274E0/
  DATA FIRST /.TRUE./
!
! LANL DEPENDENT CODE REMOVED 81.02.04
!
!***FIRST EXECUTABLE STATEMENT  GAMMA
  if (FIRST) THEN
!
! ---------------------------------------------------------------------
! INITIALIZE.  FIND LEGAL BOUNDS FOR X, AND DETERMINE THE NUMBER OF
! TERMS IN THE SERIES REQUIRED TO ATTAIN AN ACCURACY TEN TIMES BETTER
! THAN MACHINE PRECISION.
!
     NGCS = INITS (GCS, 23, 0.1*R1MACH(3))
!
     call GAMLIM (XMIN, XMAX)
     DXREL = SQRT (R1MACH(4))
!
! ---------------------------------------------------------------------
! FINISH INITIALIZATION.  START EVALUATING GAMMA(X).
!
  end if
  FIRST = .FALSE.
!
  Y = ABS(X)
  if (Y > 10.0) go to 50
!
! COMPUTE GAMMA(X) FOR ABS(X)  <=  10.0.  REDUCE INTERVAL AND
! FIND GAMMA(1+Y) FOR 0.  <=  Y  <  1. FIRST OF ALL.
!
  N = X
  if (X < 0.) N = N - 1
  Y = X - N
  N = N - 1
  GAMMA = 0.9375 + CSEVL(2.*Y-1., GCS, NGCS)
  if (N == 0) RETURN
!
  if (N > 0) go to 30
!
! COMPUTE GAMMA(X) FOR X  <  1.
!
  N = -N
  if (X  ==  0.) call XERMSG ('SLATEC', 'GAMMA', 'X IS 0', 4, 2)
  if (X  <  0. .AND. X+N-2  ==  0.) call XERMSG ('SLATEC', 'GAMMA' &
  , 'X IS A NEGATIVE INTEGER', 4, 2)
  if (X  <  (-0.5) .AND. ABS((X-AINT(X-0.5))/X)  <  DXREL) call &
  XERMSG ( 'SLATEC', 'GAMMA', &
  'ANSWER LT HALF PRECISION BECAUSE X TOO NEAR NEGATIVE INTEGER' &
  , 1, 1)
!
  DO 20 I=1,N
    GAMMA = GAMMA / (X+I-1)
 20   CONTINUE
  return
!
! GAMMA(X) FOR X  >=  2.
!
 30   DO 40 I=1,N
    GAMMA = (Y+I)*GAMMA
 40   CONTINUE
  return
!
! COMPUTE GAMMA(X) FOR ABS(X)  >  10.0.  RECALL Y = ABS(X).
!
 50   if (X  >  XMAX) call XERMSG ('SLATEC', 'GAMMA', &
     'X SO BIG GAMMA OVERFLOWS', 3, 2)
!
  GAMMA = 0.
  if (X  <  XMIN) call XERMSG ('SLATEC', 'GAMMA', &
     'X SO SMALL GAMMA UNDERFLOWS', 2, 1)
  if (X < XMIN) RETURN
!
  GAMMA = EXP((Y-0.5)*LOG(Y) - Y + SQ2PIL + R9LGMC(Y) )
  if (X > 0.) RETURN
!
  if (ABS((X-AINT(X-0.5))/X)  <  DXREL) call XERMSG ('SLATEC', &
     'GAMMA', &
     'ANSWER LT HALF PRECISION, X TOO NEAR NEGATIVE INTEGER', 1, 1)
!
  SINPIY = SIN (PI*Y)
  if (SINPIY  ==  0.) call XERMSG ('SLATEC', 'GAMMA', &
     'X IS A NEGATIVE INTEGER', 4, 2)
!
  GAMMA = -PI / (Y*SINPIY*GAMMA)
!
  return
end
