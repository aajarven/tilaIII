FUNCTION CPSI (ZIN)
!
!! CPSI computes the Psi (or Digamma) function.
!
!***LIBRARY   SLATEC (FNLIB)
!***CATEGORY  C7C
!***TYPE      COMPLEX (PSI-S, DPSI-D, CPSI-C)
!***KEYWORDS  DIGAMMA FUNCTION, FNLIB, PSI FUNCTION, SPECIAL FUNCTIONS
!***AUTHOR  Fullerton, W., (LANL)
!***DESCRIPTION
!
! PSI(X) calculates the psi (or digamma) function of X.  PSI(X)
! is the logarithmic derivative of the gamma function of X.
!
!***REFERENCES  (NONE)
!***ROUTINES CALLED  CCOT, R1MACH, XERMSG
!***REVISION HISTORY  (YYMMDD)
!   780501  DATE WRITTEN
!   890531  Changed all specific intrinsics to generic.  (WRB)
!   890531  REVISION DATE from Version 3.2
!   891214  Prologue converted to Version 4.0 format.  (BAB)
!   900315  CALLs to XERROR changed to CALLs to XERMSG.  (THJ)
!   900727  Added EXTERNAL statement.  (WRB)
!***END PROLOGUE  CPSI
  COMPLEX CPSI
  COMPLEX ZIN, Z, Z2INV, CORR, CCOT
  DIMENSION BERN(13)
  LOGICAL FIRST
  EXTERNAL CCOT
  SAVE BERN, PI, NTERM, BOUND, DXREL, RMIN, RBIG, FIRST
  DATA BERN( 1) /   .83333333333333333E-1 /
  DATA BERN( 2) /  -.83333333333333333E-2 /
  DATA BERN( 3) /   .39682539682539683E-2 /
  DATA BERN( 4) /  -.41666666666666667E-2 /
  DATA BERN( 5) /   .75757575757575758E-2 /
  DATA BERN( 6) /  -.21092796092796093E-1 /
  DATA BERN( 7) /   .83333333333333333E-1 /
  DATA BERN( 8) /  -.44325980392156863E0 /
  DATA BERN( 9) /   .30539543302701197E1 /
  DATA BERN(10) /  -.26456212121212121E2 /
  DATA BERN(11) /   .28146014492753623E3 /
  DATA BERN(12) /  -.34548853937728938E4 /
  DATA BERN(13) /   .54827583333333333E5 /
  DATA PI / 3.141592653589793E0 /
  DATA FIRST /.TRUE./
!***FIRST EXECUTABLE STATEMENT  CPSI
  if (FIRST) THEN
     NTERM = -0.30*LOG(R1MACH(3))
! MAYBE BOUND = N*(0.1*EPS)**(-1/(2*N-1)) / (PI*EXP(1))
     BOUND = 0.1171*NTERM*(0.1*R1MACH(3))**(-1.0/(2*NTERM-1))
     DXREL = SQRT(R1MACH(4))
     RMIN = EXP (MAX (LOG(R1MACH(1)), -LOG(R1MACH(2))) + 0.011 )
     RBIG = 1.0/R1MACH(3)
  end if
  FIRST = .FALSE.
!
  Z = ZIN
  X = REAL(Z)
  Y = AIMAG(Z)
  if (Y < 0.0) Z = CONJG(Z)
!
  CORR = (0.0, 0.0)
  CABSZ = ABS(Z)
  if (X >= 0.0 .AND. CABSZ > BOUND) go to 50
  if (X < 0.0 .AND. ABS(Y) > BOUND) go to 50
!
  if (CABSZ < BOUND) go to 20
!
! USE THE REFLECTION FORMULA FOR REAL(Z) NEGATIVE, ABS(Z) LARGE, AND
! ABS(AIMAG(Y)) SMALL.
!
  CORR = -PI*CCOT(PI*Z)
  Z = 1.0 - Z
  go to 50
!
! USE THE RECURSION RELATION FOR ABS(Z) SMALL.
!
 20   if (CABSZ  <  RMIN) call XERMSG ('SLATEC', 'CPSI', &
     'CPSI CALLED WITH Z SO NEAR 0 THAT CPSI OVERFLOWS', 2, 2)
!
  if (X >= (-0.5) .OR. ABS(Y) > DXREL) go to 30
  if (ABS((Z-AINT(X-0.5))/X)  <  DXREL) call XERMSG ('SLATEC', &
     'CPSI', &
     'ANSWER LT HALF PRECISION BECAUSE Z TOO NEAR NEGATIVE INTEGER', &
     1, 1)
  if (Y  ==  0.0 .AND. X  ==  AINT(X)) call XERMSG ('SLATEC', &
     'CPSI', 'Z IS A NEGATIVE INTEGER', 3, 2)
!
 30   N = SQRT(BOUND**2-Y**2) - X + 1.0
  DO 40 I=1,N
    CORR = CORR - 1.0/Z
    Z = Z + 1.0
 40   CONTINUE
!
! NOW EVALUATE THE ASYMPTOTIC SERIES FOR SUITABLY LARGE Z.
!
 50   if (CABSZ > RBIG) CPSI = LOG(Z) + CORR
  if (CABSZ > RBIG) go to 70
!
  CPSI = (0.0, 0.0)
  Z2INV = 1.0/Z**2
  DO 60 I=1,NTERM
    NDX = NTERM + 1 - I
    CPSI = BERN(NDX) + Z2INV*CPSI
 60   CONTINUE
  CPSI = LOG(Z) - 0.5/Z - CPSI*Z2INV + CORR
!
 70   if (Y < 0.0) CPSI = CONJG(CPSI)
!
  return
end
