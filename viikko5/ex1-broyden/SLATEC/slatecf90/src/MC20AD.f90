subroutine MC20AD (NC, MAXA, A, INUM, JPTR, JNUM, JDISP)
!
!! MC20AD is subsidiary to DSPLP.
!
!***LIBRARY   SLATEC
!***TYPE      DOUBLE PRECISION (MC20AS-S, MC20AD-D)
!***AUTHOR  (UNKNOWN)
!***DESCRIPTION
!
!     THIS SUBPROGRAM IS A SLIGHT MODIFICATION OF A SUBPROGRAM
!     FROM THE C. 1979 AERE HARWELL LIBRARY.  THE NAME OF THE
!     CORRESPONDING HARWELL CODE CAN BE OBTAINED BY DELETING
!     THE FINAL LETTER =D= IN THE NAMES USED HERE.
!     REVISED SEP. 13, 1979.
!
!     ROYALTIES HAVE BEEN PAID TO AERE-UK FOR USE OF THEIR CODES
!     IN THE PACKAGE GIVEN HERE.  ANY PRIMARY USAGE OF THE HARWELL
!     SUBROUTINES REQUIRES A ROYALTY AGREEMENT AND PAYMENT BETWEEN
!     THE USER AND AERE-UK.  ANY USAGE OF THE SANDIA WRITTEN CODES
!     DSPLP( ) (WHICH USES THE HARWELL SUBROUTINES) IS PERMITTED.
!
!***SEE ALSO  DSPLP
!***ROUTINES CALLED  (NONE)
!***REVISION HISTORY  (YYMMDD)
!   811215  DATE WRITTEN
!   890831  Modified array declarations.  (WRB)
!   891214  Prologue converted to Version 4.0 format.  (BAB)
!   900402  Added TYPE section.  (WRB)
!***END PROLOGUE  MC20AD
  INTEGER INUM(*), JNUM(*)
  DOUBLE PRECISION A(*),ACE,ACEP
  DIMENSION JPTR(NC)
!***FIRST EXECUTABLE STATEMENT  MC20AD
  NULL = -JDISP
!**      CLEAR JPTR
  DO 10 J=1,NC
     JPTR(J) = 0
   10 CONTINUE
!**      COUNT THE NUMBER OF ELEMENTS IN EACH COLUMN.
  DO 20 K=1,MAXA
     J = JNUM(K) + JDISP
     JPTR(J) = JPTR(J) + 1
   20 CONTINUE
!**      SET THE JPTR ARRAY
  K = 1
  DO 30 J=1,NC
     KR = K + JPTR(J)
     JPTR(J) = K
     K = KR
   30 CONTINUE
!
!**      REORDER THE ELEMENTS INTO COLUMN ORDER.  THE ALGORITHM IS AN
!        IN-PLACE SORT AND IS OF ORDER MAXA.
  DO 50 I=1,MAXA
!        ESTABLISH THE CURRENT ENTRY.
     JCE = JNUM(I) + JDISP
     if (JCE == 0) go to 50
     ACE = A(I)
     ICE = INUM(I)
!        CLEAR THE LOCATION VACATED.
     JNUM(I) = NULL
!        CHAIN FROM CURRENT ENTRY TO STORE ITEMS.
     DO 40 J=1,MAXA
!        CURRENT ENTRY NOT IN CORRECT POSITION.  DETERMINE CORRECT
!        POSITION TO STORE ENTRY.
        LOC = JPTR(JCE)
        JPTR(JCE) = JPTR(JCE) + 1
!        SAVE CONTENTS OF THAT LOCATION.
        ACEP = A(LOC)
        ICEP = INUM(LOC)
        JCEP = JNUM(LOC)
!        STORE CURRENT ENTRY.
        A(LOC) = ACE
        INUM(LOC) = ICE
        JNUM(LOC) = NULL
!        CHECK if NEXT CURRENT ENTRY NEEDS TO BE PROCESSED.
        if (JCEP == NULL) go to 50
!        IT DOES.  COPY INTO CURRENT ENTRY.
        ACE = ACEP
        ICE = ICEP
        JCE = JCEP + JDISP
   40    CONTINUE
!
   50 CONTINUE
!
!**      RESET JPTR VECTOR.
  JA = 1
  DO 60 J=1,NC
     JB = JPTR(J)
     JPTR(J) = JA
     JA = JB
   60 CONTINUE
  return
end
